buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:1.0.0'
	}
}

repositories {
	mavenCentral()
}

apply plugin: 'android'

dependencies {
	//compile "com.android.support:appcompat-v7:21.0.0"
	compile 'com.android.support:support-v4:21.0.3'

	compile 'de.cketti.library.changelog:ckchangelog:1.2.0'

	compile project(':sublibs:Android-PullToRefresh:library')
    compile project(':sublibs:OSMBonusPack')

	compile files('libs/public-transport-enabler.jar')
}

android {
	defaultConfig {
		compileSdkVersion 21
		buildToolsVersion "21.1.2"
	}

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			aidl.srcDirs = ['src']
			renderscript.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = ['assets']
		}
	}

	/*
	* To sign release build, create file gradle.properties in ~/.gradle/ with this content:
	*
	* signingStoreLocation=/home/key.store
	* signingStorePassword=xxx
	* signingKeyAlias=alias
	* signingKeyPassword=xxx
	*/
	if (project.hasProperty('signingStoreLocation') &&
			project.hasProperty('signingStorePassword') &&
			project.hasProperty('signingKeyAlias') &&
			project.hasProperty('signingKeyPassword')) {
		println "Found sign properties in gradle.properties! Signing buildâ€¦"

		signingConfigs {
			release {
				storeFile file(signingStoreLocation)
				storePassword signingStorePassword
				keyAlias signingKeyAlias
				keyPassword signingKeyPassword
			}
		}

		buildTypes.release.signingConfig = signingConfigs.release
	} else {
		buildTypes.release.signingConfig = null
	}

	// Do not abort build if lint finds errors
	lintOptions {
		abortOnError false
	}
}

