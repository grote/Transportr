buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:2.1.2'
		classpath files('libs/gradle-witness.jar')
	}
}

apply plugin: 'com.android.application'
apply plugin: 'witness'

dependencies {
	def supportVersion = '24.0.0'
	compile "com.android.support:support-v4:$supportVersion"
	compile "com.android.support:appcompat-v7:$supportVersion"
	compile "com.android.support:design:$supportVersion"
	compile "com.android.support:cardview-v7:$supportVersion"
	compile "com.android.support:recyclerview-v7:$supportVersion"
	compile "com.android.support:preference-v14:$supportVersion"

	compile 'com.github.orangegangsters:swipy:1.2.3'
	compile 'de.cketti.library.changelog:ckchangelog:1.2.2'
	compile 'org.apmem.tools:layouts:1.10'
	compile 'com.mikepenz:materialdrawer:5.3.0'
	compile 'com.mikepenz:aboutlibraries:5.6.7'
	compile 'org.osmdroid:osmdroid-android:5.2'

	compile project(':sublibs:public-transport-enabler:enabler')
	debugCompile 'org.slf4j:slf4j-api:1.7.14'
	debugCompile 'com.github.tony19:logback-android-classic:1.1.1-4'
	debugCompile 'com.github.tony19:logback-android-core:1.1.1-4'
}

dependencyVerification {
	verify = [
			'com.android.support:support-v4:b6d3994d4bdea0ef2a221ecf9e0ddf150b99af189211c36418c81e248f3fed99',
			'com.android.support:appcompat-v7:a30898bbec4d506f821055a362d62820b18f29e9cf0c077817e46cb5a03cde94',
			'com.android.support:design:c93faa6d9d2db76243e45d0f6fe9598210e1456b4c14b2c53a6268cf2d0e6423',
			'com.android.support:cardview-v7:6ea6747bd197c6b8e7c79f98d6a6d1b75599fb9110a2fffe95e1b095905a5658',
			'com.android.support:recyclerview-v7:6325470bae1f938d0a9d080d1b62b74e2e734d4d182555b49c91840eaa30fe3f',
			'com.android.support:preference-v14:8d404439d20382cba8f18b875505a219b4eefc36b0028c411b1dbfd0194e8a50',
			'com.github.orangegangsters:swipy:153d03ab51b7c3672e0dfb60963dc4b983c3bf0fd39adbff576cd5179b8edbda',
			'de.cketti.library.changelog:ckchangelog:e113de0f4bc42b983dd6b690b67d76f29975167c2204e3792831961740889d78',
			'org.apmem.tools:layouts:25f878e7faa5c384ed0861889104e5f10a40e5682eaf80c9abb47559c367b348',
			'com.mikepenz:materialdrawer:3a37437ab8cdd8ad66b770217cd84fd825b0d368eb73528fc8282ea7c9f35e1d',
			'com.mikepenz:aboutlibraries:deebfe12bd5cfcb1c3b18bcaf475cfcb331c5edc59ee31650e69f05f161c8f97',
			'org.osmdroid:osmdroid-android:22d6aacad7d61f784b4cb849f819751e62f1de87396f21b6b438f24eb02c056a',
			'com.android.support:support-annotations:0790aced70e3ce82a6e466f0d4908cdd9e21bd925b4107437441d201f595a908',
			'com.android.support:support-vector-drawable:8ca786031bf3b9dc542d7fbf2b8b9683ca7ccf2f63d4303fb239bc9d561a497c',
			'com.android.support:animated-vector-drawable:7b8badd14c6be764a44aea2d3ba31ad8c0328e94c25305d76332a2d7d73eddd6',
			'com.android.support:preference-v7:6d82e5af968f2fc685bdcbffa659da779ba504a0e44bdd3fdc0041f716d2eb34',
			'com.mikepenz:fastadapter:43bb0c5bf44ea2dc167a456bb87dd27cd33b184ea1f6a5b4a09e521013821461',
			'com.mikepenz:iconics-core:b2ae00051deda24c3ed24cc6dc3857434bf53a09d734554028ca6743f49e9e7f',
			'com.mikepenz:materialize:575195b2fa5b2414fb14a59470ee21d8a8cd8355b651e0cf52e477e3ff1cd96c',
			'com.google.guava:guava:d664fbfc03d2e5ce9cab2a44fb01f1d0bf9dfebeccc1a473b1f9ea31f79f6f99',
			'org.slf4j:slf4j-api:0aee9a77a4940d72932b0d0d9557793f872e66a03f598e473f45e7efecdccf99',
			'com.google.code.findbugs:jsr305:bec0b24dcb23f9670172724826584802b80ae6cbdaba03bdebdef9327b962f6a',
			'org.json:json:055be110a570f9cda3eba8d70a006ff46c77a048bc67868524879211c48b330a',
			'net.sf.kxml:kxml2:f264dd9f79a1fde10ce5ecc53221eff24be4c9331c830b7d52f2f08a7b633de2',
	]
}

allprojects {
	repositories {
		jcenter()
	}
}

android {
	defaultConfig {
		versionCode 37
		versionName "1.1.3"

		compileSdkVersion 24
		targetSdkVersion 24
		buildToolsVersion "23.0.2" // travis does not have >= 23.0.3
		minSdkVersion 14
	}

	buildTypes {
		release {
			resValue "string", "app_name", "Transportr"
		}

		debug {
			applicationIdSuffix ".debug"
			versionNameSuffix "-debug"
			resValue "string", "app_name", "Transportr Devel"
		}
	}

	dexOptions {
		preDexLibraries !project.hasProperty('buildServer')
		incremental !project.hasProperty('buildServer')
		javaMaxHeapSize "1g"
	}

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			aidl.srcDirs = ['src']
			renderscript.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = ['assets']
		}
	}

	/*
	* To sign release build, create file gradle.properties in ~/.gradle/ with this content:
	*
	* signingStoreLocation=/home/key.store
	* signingStorePassword=xxx
	* signingKeyAlias=alias
	* signingKeyPassword=xxx
	*/
	if (project.hasProperty('signingStoreLocation') &&
			project.hasProperty('signingStorePassword') &&
			project.hasProperty('signingKeyAlias') &&
			project.hasProperty('signingKeyPassword')) {
		println "Found sign properties in gradle.properties! Signing buildâ€¦"

		signingConfigs {
			release {
				storeFile file(signingStoreLocation)
				storePassword signingStorePassword
				keyAlias signingKeyAlias
				keyPassword signingKeyPassword
			}
		}

		buildTypes.release.signingConfig = signingConfigs.release
		buildTypes.debug.signingConfig = signingConfigs.release
	} else {
		buildTypes.release.signingConfig = null
	}

	lintOptions {
		abortOnError false
		checkReleaseBuilds false
		disable 'MissingTranslation'
		htmlReport true
	}
}

