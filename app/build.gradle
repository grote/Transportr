apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'witness'
apply from: 'witness.gradle'

android {

	defaultConfig {
		versionCode 122
		versionName "2.1.4"

		applicationId "de.grobox.liberario"
		minSdkVersion 16
		compileSdkVersion 31
		targetSdkVersion 30

		testInstrumentationRunner 'de.grobox.transportr.MockTestRunner'
		javaCompileOptions {
			annotationProcessorOptions {
				arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
			}
		}
	}

	buildTypes {
		release {
			resValue "string", "app_name", "Transportr"
			shrinkResources true
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
		}

		debug {
			applicationIdSuffix ".debug"
			versionNameSuffix "-debug"
			resValue "string", "app_name", "Transportr Devel"
			shrinkResources false
			minifyEnabled false
			multiDexEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
			testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt', 'proguard-test.txt'
			multiDexKeepProguard file('proguard-multidex.txt')

			lintOptions {
				disable 'ProtectedPermissions'
			}
		}
	}

	compileOptions {
		targetCompatibility JavaVersion.VERSION_1_8
		sourceCompatibility JavaVersion.VERSION_1_8
	}

	dexOptions {
		preDexLibraries !project.hasProperty('buildServer')
		javaMaxHeapSize "1g"
	}

	sourceSets {
		androidTest {
			assets.srcDirs += files("$projectDir/schemas".toString())
		}
	}

	/*
	* To sign release build, create file gradle.properties in ~/.gradle/ with this content:
	*
	* signingStoreLocation=/home/key.store
	* signingStorePassword=xxx
	* signingKeyAlias=alias
	* signingKeyPassword=xxx
	*/
	if (!project.hasProperty('buildServer') && project.hasProperty('signingStoreLocation') && project.hasProperty('signingStorePassword') &&
			project.hasProperty('signingKeyAlias') && project.hasProperty('signingKeyPassword')) {
		signingConfigs {
			release {
				storeFile file(signingStoreLocation)
				storePassword signingStorePassword
				keyAlias signingKeyAlias
				keyPassword signingKeyPassword
			}
		}
		buildTypes.release.signingConfig = signingConfigs.release
		buildTypes.debug.signingConfig = signingConfigs.release
	} else {
		buildTypes.release.signingConfig = null
	}

	lintOptions {
		checkReleaseBuilds false
		disable 'MissingTranslation', // don't put too much pressure on Transifex contributors
				'InvalidPackage',
				'VectorDrawableCompat', //todo: investigate about VectorDrawableCompat
				'TrustAllX509TrustManager', // accept certificates as implemented in PTE
				'GradleDependency', // don't complain about old library versions
				'IconDensities', //todo: migrate all (network) icons to vectors
				'PrivateResource' //todo: investigate about private Material design resources (e.g. CardView style)
	}
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

	implementation 'com.google.android.material:material:1.3.0'
	implementation 'androidx.appcompat:appcompat:1.3.0'
	implementation 'androidx.activity:activity:1.2.3'
	implementation('androidx.fragment:fragment:1.1.0!!') {
		because 'issues with MapBox SDK onDestroy after upgrading to 1.2.0-alpha02 or newer versions'
	}
	implementation('androidx.preference:preference:1.1.0') {
		because 'newer version requires fragment v1.2.4'
	}
	implementation 'androidx.cardview:cardview:1.0.0'
	implementation 'androidx.recyclerview:recyclerview:1.2.0'
	implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
	implementation 'androidx.lifecycle:lifecycle-livedata:2.3.1'
	implementation 'androidx.lifecycle:lifecycle-common-java8:2.3.1'
	implementation 'androidx.room:room-runtime:2.3.0'
	kapt 'androidx.room:room-compiler:2.3.0'
	implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'

	implementation 'com.squareup.okhttp3:okhttp:3.12.12'
	implementation 'com.github.omadahealth:swipy:1.2.3'
	implementation 'de.cketti.library.changelog:ckchangelog:1.2.2'
	implementation 'com.google.android:flexbox:0.3.2'
	implementation 'com.mikepenz:materialdrawer:6.1.1'
	implementation 'com.mikepenz:aboutlibraries:6.2.0'
	implementation "com.mikepenz:fastadapter:$fastadapterVersion"
	implementation "com.mikepenz:fastadapter-commons:$fastadapterVersion"
	implementation "com.mikepenz:fastadapter-extensions-expandable:$fastadapterVersion"
	implementation 'uk.co.samuelwall:material-tap-target-prompt:2.14.0'
	implementation('org.maplibre.gl:android-sdk:9.5.2') {
		exclude group: 'com.google.android.gms' // no proprietary Google libraries
	}
	// only added because of lint bug Timber 4.6.0
	implementation 'com.jakewharton.timber:timber:4.7.0'

	implementation "com.google.dagger:dagger:$daggerVersion"
	kapt "com.google.dagger:dagger-compiler:$daggerVersion"

	implementation('com.google.guava:guava:29.0-android') {
		exclude module: 'listenablefuture'
		exclude module: 'failureaccess'
		exclude group: 'com.google.j2objc'
	}
	implementation('com.gitlab.opentransitmap:public-transport-enabler:348d7340') {
		exclude group: 'com.google.guava' // included above
		exclude group: 'org.json', module: 'json' // provided by Android
		exclude group: 'net.sf.kxml', module: 'kxml2' // provided by Android
	}
	debugImplementation 'org.slf4j:slf4j-api:1.7.32'
	debugImplementation 'com.github.tony19:logback-android:1.1.1-12'

	testImplementation 'junit:junit:4.13.1'
	testImplementation 'org.mockito:mockito-core:3.0.0'
	testImplementation 'androidx.arch.core:core-testing:2.1.0'

	androidTestImplementation 'androidx.test:core:1.3.0'
	androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
	androidTestImplementation 'androidx.test:rules:1.3.0'
	androidTestImplementation 'androidx.test.ext:junit:1.1.2'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
	androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
	androidTestImplementation('tools.fastlane:screengrab:1.1.0') {
		exclude group: 'androidx.test.uiautomator' //requires minSDK >= 18
	}
	kaptAndroidTestDebug "com.google.dagger:dagger-compiler:$daggerVersion"
}
